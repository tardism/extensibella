
/*
  Abella requires declaring the imported kinds.  I'm not sure why.
*/

Kind $lib__nat   type.
Import "nats".

Kind $lib__bool   type.
Import "bools".


/*
  The term `$posInt N` is the positive number `N`.  The term
  `$negSuccInt N` is the negative number `(-N - 1)`.  For example,
  `$negsuccInt $zero` is -1.  This encoding gives us a single encoding
  for the integer 0, where an encoding with just a `$negInt`
  constructor (rather than `$negSuccInt`) would give us positive and
  negative zeroes.

  Similarly, an encoding which didn't use the `nat` type and simply
  had constructors for zero, adding a positive one, and subtracting a
  negative one, would not have unique encodings for any number.
*/

Kind $lib__integer   type.
Type $posInt   $lib__nat -> $lib__integer.
Type $negSuccInt   $lib__nat -> $lib__integer.

%The definition using $is_nat is NOT helpful
Define is_integer : $lib__integer -> prop by
  is_integer ($posInt $zero);
  is_integer ($posInt ($succ N)) := is_integer ($posInt N);
  is_integer ($negSuccInt $zero);
  is_integer ($negSuccInt ($succ N)) := is_integer ($negSuccInt N).



%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%                            IS RELATIONS                            %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

Theorem $is_nat_$$$$$_to_$$$$$_is_integer_posInt : forall N,
  $is_nat N -> is_integer ($posInt N).
induction on 1. intros. case H1.
  %Subgoal 1
   search.
  %Subgoal 2
   apply IH to H2. search.

Theorem $is_nat_$$$$$_to_$$$$$_is_integer_negSuccInt : forall N,
  $is_nat N -> is_integer ($negSuccInt N).
induction on 1. intros. case H1.
  %Subgoal 1
   search.
  %Subgoal 2
   apply IH to H2. search.

Theorem $is_integer_posInt_$$$$$_to_$$$$$_is_nat : forall N,
  is_integer ($posInt N) -> $is_nat N.
induction on 1. intros. case H1.
  %Subgoal 1
   search.
  %Subgoal 2
   apply IH to H2. search.

Theorem $is_integer_negSuccInt_$$$$$_to_$$$$$_is_nat : forall N,
  is_integer ($negSuccInt N) -> $is_nat N.
induction on 1. intros. case H1.
  %Subgoal 1
   search.
  %Subgoal 2
   apply IH to H2. search.

Theorem $is_integer_posInt_$$$$$_to_$$$$$_negSuccInt : forall N,
  is_integer ($posInt N) -> is_integer ($negSuccInt N).
intros. apply $is_integer_posInt_$$$$$_to_$$$$$_is_nat to H1.
backchain $is_nat_$$$$$_to_$$$$$_is_integer_negSuccInt.

Theorem $is_integer_negSuccInt_$$$$$_to_$$$$$_posInt : forall N,
  is_integer ($negSuccInt N) -> is_integer ($posInt N).
intros. apply $is_integer_negSuccInt_$$$$$_to_$$$$$_is_nat to H1.
backchain $is_nat_$$$$$_to_$$$$$_is_integer_posInt.

Theorem $is_integer_posInt_or_negSuccInt : forall N,
  is_integer N -> exists A, N = $posInt A \/ N = $negSuccInt A.
intros. case H1.
  %Subgoal 1
   search.
  %Subgoal 2
   search.
  %Subgoal 3
   search.
  %Subgoal 4
   search.

Theorem extensibella-$-stdLib-$-is_integer_eq_or_not : forall I1 I2,
  is_integer I1 -> is_integer I2 ->
  I1 = I2 \/ (I1 = I2 -> false).
induction on 1. intros. case H1.
  %Subgoal 1
   case H2.
     %Subgoal 1.1
      left. search.
     %Subgoal 1.2
      right. intros. case H4.
     %Subgoal 1.3
      right. intros. case H3.
     %Subgoal 1.4
      right. intros. case H4.
  %Subgoal 2
   case H2.
     %Subgoal 2.1
      right. intros. case H4.
     %Subgoal 2.2
      apply IH to H3 H4. case H5.
        %Subgoal 2.2.1
         left. search.
        %Subgoal 2.2.2
         right. intros. case H7. backchain H6.
     %Subgoal 2.3
      right. intros. case H4.
     %Subgoal 2.4
      right. intros. case H5.
  %Subgoal 3
   case H2.
     %Subgoal 3.1
      right. intros. case H3.
     %Subgoal 3.2
      right. intros. case H4.
     %Subgoal 3.3
      left. search.
     %Subgoal 3.4
      right. intros. case H4.
  %Subgoal 4
   case H2.
     %Subgoal 4.1
      right. intros. case H4.
     %Subgoal 4.2
      right. intros. case H5.
     %Subgoal 4.3
      right. intros. case H4.
     %Subgoal 4.4
      apply IH to H3 H4. case H5.
        %Subgoal 4.4.1
         left. search.
        %Subgoal 4.4.2
         right. intros. case H7. backchain H6.

